class_name RoomGeneration
extends Node2D

@export var first_room_scene: PackedScene
@export var room_scenes: Array[PackedScene]

@export var player: CharacterBody2D

@export var map_size: int = 7
@export var rooms_to_generate: int = 12

var room_count: int = 0
var map: Array[bool]
var rooms: Array[Room]
var room_pos_offset: float = 160

var first_room_x: int = 3
var first_room_y: int = 3
var first_room: Room

var room_scene: PackedScene = preload("res://Scenes/Rooms/room_template.tscn")

func _ready() -> void:
	_generate()
	for x in range(map_size):
		var line: String = ""
		for y in range(map_size):
			line += "# " if _get_map(x, y) else "  "
		print(line)

func _generate():
	room_count = 0
	map.resize(map_size * map_size)
	_check_room(first_room_x, first_room_y, Vector2.ZERO, true)
	_instantiate_rooms()
	
func _check_room(x: int, y: int, desired_direction: Vector2, is_first_room: bool = false):
	if room_count >= rooms_to_generate:
		return
	if x < 0 or x > map_size - 1 or y < 0 or y > map_size - 1:
		return
	if _get_map(x, y):
		return
	
	room_count += 1
	_set_map(x, y, true)
	
	var go_north: bool = randf() > (0.2 if desired_direction == Vector2.UP else 0.8)
	if go_north or is_first_room:
		_check_room(x, y-1, Vector2.UP if is_first_room else desired_direction)
		
	var go_south: bool = randf() > (0.2 if desired_direction == Vector2.DOWN else 0.8)
	var go_east: bool = randf() > (0.2 if desired_direction == Vector2.RIGHT else 0.8)
	var go_west: bool = randf() > (0.2 if desired_direction == Vector2.LEFT else 0.8)
	
	if go_south or is_first_room:
		_check_room(x, y+1, Vector2.DOWN if is_first_room else desired_direction)
	if go_east or is_first_room:
		_check_room(x+1, y, Vector2.RIGHT if is_first_room else desired_direction)	
	if go_west or is_first_room:
		_check_room(x-1, y, Vector2.LEFT if is_first_room else desired_direction)
	
func _instantiate_rooms():
	for x in range(map_size):
		for y in range(map_size):
			if _get_map(x, y) == false:
				continue
			
			var room: Room
			var is_first_room: bool = first_room_x == x and first_room_y == y
			
			if is_first_room:
				room = first_room_scene.instantiate()
			else:
				room = room_scenes[randi_range(0, len(room_scenes) - 1)].instantiate()
			
			## GREÅ KA OVDJE!
			get_tree().root.get_node("/root/Main").add_child.call_deferred(room)
			rooms.append(room)
			room.global_position = Vector2(x, y) * room_pos_offset
			
			if is_first_room:
				first_room = room
			
			room.initialize()
			
	for room in rooms:
		var map_pos: Vector2 = _get_map_index(room)
		var x = map_pos.x
		var y = map_pos.y
		
		if y > 0 and _get_map(x, y - 1):
			room.set_neighbor.call_deferred(Room.Direction.NORTH, get_room_from_map(x, y - 1))
			
		if y < map_size - 1 and _get_map(x, y + 1):
			room.set_neighbor.call_deferred(Room.Direction.SOUTH, get_room_from_map(x, y + 1))
		
		if x < map_size - 1 and _get_map(x + 1, y):
			room.set_neighbor.call_deferred(Room.Direction.EAST, get_room_from_map(x + 1, y))
		
		if x > 0 and _get_map(x - 1, y):
			room.set_neighbor.call_deferred(Room.Direction.WEST, get_room_from_map(x - 1, y))
			
	first_room.player_enter.call_deferred(Room.Direction.NORTH, player, true)
	
func _get_map(x: int, y: int) -> bool:
	return map[x + y * map_size]
	
func _set_map(x: int, y: int, value: bool):
	map[x + y * map_size] = value

func _get_map_index(room: Room) -> Vector2i:
	return Vector2i(room.global_position / room_pos_offset)
	
func get_room_from_map(x: int, y: int) -> Room:
	for room in rooms:
		var pos = _get_map_index(room)
		if pos.x != x or pos.y != y:
			continue
		return room
	return null
